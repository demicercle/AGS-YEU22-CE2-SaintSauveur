

struct InteractiveGUI
{
  GUIControl* control;
  int cursor;
};

int DEFAULT_GRAPHIC = 2061;
int TALK_GRAPHIC = 2058;
int INTERACT_GRAPHIC = 2057;
int WALK_GRAPHIC = 2054;
int INSPECT_GRAPHIC = 2056;

int loops;
bool hasAction;

int guiCount;
InteractiveGUI guis[10];

function game_start()
{
  player.Clickable = false;
  
  guiCount = 8;
  guis[0].control = bPianoA1;
  guis[0].cursor = INTERACT_GRAPHIC;
  guis[1].control = bPianoB1;
  guis[1].cursor = INTERACT_GRAPHIC;
  guis[2].control = bPianoC1;
  guis[2].cursor = INTERACT_GRAPHIC;
  guis[3].control = bPianoD1;
  guis[3].cursor = INTERACT_GRAPHIC;
  guis[4].control = bPianoE1;
  guis[4].cursor = INTERACT_GRAPHIC;
  guis[5].control = bPianoF1;
  guis[5].cursor = INTERACT_GRAPHIC;
  guis[6].control = bPianoG1;
  guis[6].cursor = INTERACT_GRAPHIC;
  guis[7].control = bPianoC2;
  guis[7].cursor = INTERACT_GRAPHIC;
}

function repeatedly_execute()
{
  int currGraphic = mouse.GetModeGraphic(mouse.Mode);
  int newGraphic = DEFAULT_GRAPHIC;
  
  hasAction = false;
  
  if (!IsGamePaused())
  {
    int mx = mouse.x;
    int my = mouse.y;
    int lt = GetLocationType(mx, my);
    
    if(lt == eLocationHotspot)
    {
      Hotspot *h = Hotspot.GetAtScreenXY(mx, my);
      if(h.Enabled) 
      {
        hasAction = true;
        newGraphic = h.GetProperty("CursorImage");
        if(newGraphic == 0) newGraphic = WALK_GRAPHIC;
      }
    }
    else if (lt == eLocationObject)
    {
      Object *o = Object.GetAtScreenXY(mx, my);
      if(o.Clickable)
      {
        hasAction = true;
        newGraphic = o.GetProperty("CursorImage");
        if(newGraphic == 0) newGraphic = INTERACT_GRAPHIC;
      }
    }
    else if (lt == eLocationCharacter)
    {
      Character *c = Character.GetAtScreenXY(mx, my);
      if(c.Clickable)
      {
        hasAction = true;
        newGraphic = c.GetProperty("CursorImage");
        if(newGraphic == 0) newGraphic = TALK_GRAPHIC;
      }
    }
    else
    {
      for(int i = 0; i < guiCount; i++)
      {
        if(guis[i].control != null && GUIControl.GetAtScreenXY(mouse.x, mouse.y) == guis[i].control)
        {
          newGraphic = guis[i].cursor;
        }
      }
    }
  }
  
  if (player.ActiveInventory != null)
  {
    if(hasAction && loops%6 == 0)
    {
      Mouse.ChangeModeGraphic(eModeUseinv, 0);
    }
    else if (player.ActiveInventory != null)
    {
      Mouse.ChangeModeGraphic(eModeUseinv, player.ActiveInventory.CursorGraphic);
    }
  }
  
  if(player.ActiveInventory == null)
  {
    // change?
    if (newGraphic != currGraphic)
      mouse.ChangeModeGraphic(mouse.Mode, newGraphic);
  }
  
  loops++;
}